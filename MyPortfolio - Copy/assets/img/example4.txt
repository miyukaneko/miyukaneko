// Example 4.4 Adjustable Array Version
//count
int count = 20;
int frame = 40;

//arrays 1-13
float[] posX = new float[count];//1
float[] posY = new float[count];//2
float[] speedX = new float[count];//3
float[] speedY = new float[count];//4
float[] sizeW = new float[count];//5
float[] sizeH = new float[count];//6
int[] red = new int[count];//7 petal color R
int[] green = new int[count];//8 petal color G
int[] blue = new int[count];//9 petal color B
float[] scalez = new float[count];//10 scale
float [] alpha = new float[count];//11 transparancy
float [] rotate = new float[count];//12 rotation
float [] grow = new float[count];//13 size change

void setup() { 
  size(800, 800);
  colorMode(RGB,100);
  for (int i=0; i < posX.length ; i++) { //i = index
    posX[i] = width/2; 
    posY[i] = height/2; 
    speedX[i] = random(-5, 5); 
    speedY[i] = random(-5, 5); 
    sizeW[i] = random(5, 50); 
    sizeH[i] = random(5, 50); 
    red[i] = int(random(255));
    green[i] = int(random(255));
    blue[i] = int(random(255));
    scalez[i] = random(.25,2);
    alpha[i] = random(10,255);
    rotate[i] = random(255);
    grow[i] = random(0.01,0.1);
  }
} 


void draw() { 
  background(80);  
  
  for (int i = 0; i < posX.length; i++) { 
    //update all positions 
    posX[i] += speedX[i]; 
    posY[i] += speedY[i]; 
    //draw all balls 
    fill(red[i],green[i],blue[i],alpha[i]); 
    flower(posX[i], posY[i], rotate[i], scalez[i],red[i],green[i],blue[i], alpha[i]);
    rotate[i]++;
    
    scalez[i] += grow[i];
    if (scalez[i] > 2 || scalez[i] < 0.3)
      grow[i] = -grow[i];
    
    if (posX[i] < frame+sizeW[i]/2 || posX[i] > (width-frame)-sizeW[i]/2 ) { 
      speedX[i] = -speedX[i]; 
    } 
    if (posY[i] < frame+sizeW[i]/2 || posY[i] > (height-frame)-sizeW[i]/2) { 
      speedY[i] = -speedY[i];
    }
  }
  //if (frameCount < 500 ) {
  //  saveFrame("images/face-######.png");
  //}
} 



void flower(float lox, float loy, float rot, float sz, int r, int g, int b, float alp) {
  pushMatrix();
  
  translate(lox,loy);
  rotate(radians(rot));
  scale(sz);
  smooth();
  noStroke();
  
  for (int i = 0; i < 5; i++) {
    fill(r, g, b, alp);
    ellipse(0, -40, 50, 50);
    rotate(radians(72));
  }
  
  fill(#fff9bb); // light yellow
  ellipse(0, 0, 50, 50);//center circle

  popMatrix();
}